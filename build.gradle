plugins {
    id "com.dorongold.task-tree" version "1.2.2"
}

allprojects {
    group "uchuhimo"
    version "0.1"

    repositories {
        //mavenCentral()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
        jcenter()
    }
    
    task wrapper(type: Wrapper) {
        gradleVersion = "3.2"
        distributionType = Wrapper.DistributionType.ALL
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "scala"
    apply plugin: "application"

    mainClassName = findProperty("mainClass") ?: ""

    sourceCompatibility = 1.8

    ext {
//        scalaMinorVersion = 11
//        scalaPatchVersion = 11
        scalaMinorVersion = 12
        scalaPatchVersion = 2
    }

    dependencies {
        compile "org.scala-lang:scala-library:2.$scalaMinorVersion.$scalaPatchVersion"
        testCompile "org.scalatest:scalatest_2.$scalaMinorVersion:3.0.3"
        testCompile "junit:junit:4.12"
    }

    sourceSets {
        main {
            scala {
                srcDir sourceSets.main.java.srcDirs
            }
            java {
                srcDirs = []
            }
        }
        test {
            scala {
                srcDir sourceSets.test.java.srcDirs
            }
            java {
                srcDirs = []
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    tasks.withType(ScalaCompile) {
        options.encoding = "UTF-8"
    }
    
    tasks.withType(Test) {
        testLogging.showStandardStreams = true
    }
}
